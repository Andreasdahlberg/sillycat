# -*- coding: utf-8 -*
#
# This file is part of SillyCat Development Tools.
#
# SillyCat Development Tools is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# SillyCat Development Tools is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with SillyCat Development Tools.  If not, see <http://www.gnu.org/licenses/>.

__author__ = 'andreas.dahlberg90@gmail.com (Andreas Dahlberg)'

import os
import sys

colors = {}
colors['cyan']   = '\033[36m'
colors['purple'] = '\033[35m'
colors['blue']   = '\033[34m'
colors['yellow'] = '\033[33m'
colors['green']  = '\033[32m'
colors['red']    = '\033[31m'
colors['end']    = '\033[0m'

#If the output is not a terminal, remove the colors
if not sys.stdout.isatty():
    for key, value in colors.iteritems():
        colors[key] = ''

compile_source_message = '{}Compiling {}==> {}$TARGET{}'.format(
    colors['green'], colors['end'], colors['blue'], colors['end'])

flashing_message = '{}Flashing {}==> {}$SOURCE{}'.format(
    colors['yellow'], colors['end'], colors['blue'], colors['end'])

linking_message = '{}Linking {}==> {}$TARGET{}'.format(
    colors['purple'], colors['end'], colors['blue'], colors['end'])

hex_message = '{}Generating HEX {}==> {}$TARGET{}'.format(
    colors['cyan'], colors['end'], colors['blue'], colors['end'])

eeprom_message = '{}Generating EEPROM {}==> {}$TARGET{}'.format(
    colors['cyan'], colors['end'], colors['blue'], colors['end'])

font_message = '{}Generating font {}==> {}$TARGET{}'.format(
    colors['cyan'], colors['end'], colors['blue'], colors['end'])

testing_message = 'Unit testing...'
PROJECT_NAME = 'sillycat'

#Target variables
mcu_vars = Variables('mcu_target.py')
mcu_vars.Add('MCU', 'Target processor')
mcu_vars.Add('F_CPU', 'Target frequency in Hz')
mcu_vars.Add('PORT', 'Port for the programmer')
mcu_vars.Add('TOOL', 'The programmer to use')
mcu_vars.Add('OPTIMIZATION', 'The optimization level to use for compilation(0, 1, 2, 3, s)', 's')
mcu_vars.Add('STD', 'The C Dialect to use', 'c99')

avr_ccflags = [
    '-std=${STD}',
    '-mmcu=${MCU}',
    '-O${OPTIMIZATION}',
    '-Wextra',
    '-Wall',
    '-ffunction-sections',
    '-fdata-sections'
]

avr_cppdefines = {
  'F_CPU' : '${F_CPU}UL',
}

common_env = Environment(
    tools=['avr', 'font'],
    CC = 'avr-gcc',
    variables = mcu_vars,
    CCFLAGS = avr_ccflags,
    CPPDEFINES = avr_cppdefines
)

test_env = Environment(
    tools=['default', 'coverage'],
    CC='gcc',
    CCFLAGS=['-g', '-O0'],
    CPPDEFINES='UNIT_TESTING',
    LIBS=['cmocka', 'gcov']
)

if ARGUMENTS.get('VERBOSE') != 'true':
    common_env['CCCOMSTR'] = compile_source_message
    common_env['AVRDUDE_ELF_COMSTR'] = linking_message
    common_env['AVRDUDE_HEX_COMSTR'] = hex_message
    common_env['AVRDUDE_EEP_COMSTR'] = eeprom_message
    common_env['FONT_COMSTR'] = font_message
    flash_quell = '-q -q'

    test_env['CCCOMSTR'] = compile_source_message
    test_env['LINKCOMSTR'] = linking_message
    test_env['COVERAGE_COMSTR'] = testing_message
else:
    flash_quell = ''


Help(mcu_vars.GenerateHelpText(common_env))

TARGETS = ['node', 'main']

MODES = {
    'debug': ['DEBUG_ENABLE'],
    'release': ['']
}


Help('\nTargets:\n')
for target in TARGETS:
    for mode in MODES:
        env = common_env.Clone()
        env.Append(CPPDEFINES=MODES[mode])

        hex, elf, eep = env.SConscript('src/SConscript',
            duplicate=0,
            variant_dir='build/{}/{}'.format(mode, target),
            exports={'env': env, 'project_name': PROJECT_NAME, 'mode': mode, 'target': target})

        alias_build_name = 'build-{}-{}'.format(target, mode)
        Alias(alias_build_name, hex)

        alias_eeprom_name = 'eeprom-{}-{}'.format(target, mode)
        Alias(alias_eeprom_name, eep)

        flash_cmd = 'avrdude -c {} -p {} -P {} -Uflash:w:{} -B 0.5 {}'.format(
            env['TOOL'], env['MCU'], env['PORT'],'${SOURCE}', flash_quell)

        flash_name = 'flash-{}-{}'.format(target, mode)
        flash = Command(flash_name, hex,  Action(flash_cmd, flashing_message))

        build_help_text = '    {}: Build {} in {} mode.\n'.format(alias_build_name, target, mode)
        Help(build_help_text)

        flash_help_text = '    {}: Flash {} in {} mode.\n'.format(flash_name, target, mode)
        Help(flash_help_text)

        eeprom_help_text = '    {}: Create EEPROM file for {} in {} mode.\n'.format(alias_eeprom_name, target, mode)
        Help(eeprom_help_text)

        size_cmd = 'avr-size {} --mcu={} -C'.format(elf[0], env['MCU'])
        size_name = 'size-{}-{}'.format(target, mode)
        size = Command(size_name, elf,  size_cmd)

        size_help_text = '    {}: Get section sizes and total size for {} in {} mode.\n'.format(size_name, target, mode)
        Help(size_help_text)

        Help('\n')

def add_test(module_under_test, mocks):
    unit_env = test_env.Clone()

    module_under_test_name = module_under_test.split(os.path.sep)[-1]

    unit_test_dir = os.path.join('build', 'unit_tests', module_under_test_name, 'unit')
    unit_test_sconscript = os.path.join('tests', module_under_test, 'SConscript')
    test_object = unit_env.SConscript(unit_test_sconscript,
        variant_dir=unit_test_dir,
        duplicate=0,
        exports={'env': unit_env, 'project_name': PROJECT_NAME}
    )

    unit_env.Append(CCFLAGS='--coverage')

    module_under_test_dir = os.path.join('build', 'unit_tests', module_under_test_name, 'module')
    module_under_test_sconscript = os.path.join('src', module_under_test, 'SConscript')
    module_object = unit_env.SConscript(module_under_test_sconscript,
        variant_dir=module_under_test_dir,
        duplicate=0,
        exports={'env': unit_env, 'project_name': PROJECT_NAME}
    )

    coverage_file = os.path.join(module_under_test_dir, '{}.gcno'.format(module_under_test_name))
    Clean(module_object, coverage_file)

    runner_name = os.path.join('build', 'unit_tests', module_under_test_name, 'TestRunner')
    runner = unit_env.Program(target=runner_name, source=[test_object, module_object]+mock_object)

    return runner

mock_object = test_env.SConscript(
    'tests/mocks/SConscript',
    variant_dir='build/unit_tests/mocks',
    duplicate=0,
    exports={'env': test_env, 'project_name': PROJECT_NAME}
)

modules = [
    os.path.join('utility', 'CRC'),
    os.path.join('utility', 'List'),
    os.path.join('utility', 'FIFO'),
    os.path.join('main', 'encoder'),
    os.path.join('main', 'interface'),
    os.path.join('main', 'sensor'),
    os.path.join('main', 'driver', 'MCUTemperature'),
    os.path.join('common', 'timer'),
    os.path.join('common', 'com'),
    os.path.join('common', 'event'),
]

tests = []
for module in modules:
    tests.append(add_test(module, mock_object))

test_env.Alias('build-tests', tests)
test_env.Coverage(source=tests, target='coverage')
